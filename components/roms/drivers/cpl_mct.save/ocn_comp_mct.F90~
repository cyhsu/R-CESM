!BOP
!   !MODULE: ocn_comp_mct
!   !INTERFACE:

module ocn_comp_mct

!   !DESCRIPTION:
!       This is the main driver for the Regional Ocean Modeling System (ROMS)
!
!   !REVISION HISTORY:
!       Apr 23, 2012 - Raffaele Montuoro <rmontuoro@tamu.edu> - initial release
!       Aug 09, 2012 - Raffaele Montuoro <rmontuoro@tamu.edu> - added wind/velocities rotation
!       Apr 08, 2013 - Raffaele Montuoro <rmontuoro@tamu.edu> - included modifications by Tony Craig
!
!   !USES:
    use mct_mod
    use esmf

    use seq_cdata_mod
    use seq_flds_mod
    use seq_infodata_mod
    use seq_timemgr_mod
    use perf_mod
    use shr_file_mod
    use shr_sys_mod, only : shr_sys_flush


    ! ROMS modules
    use mod_kinds        , only : r8

    use mod_parallel,      only : ocn_root => MyMaster, &
                                  ocn_tile => MyRank,   &
                                  log_task => OutThread

    use mod_iounits,       only : stdout

    use mod_scalars,       only : ROMS_exit_flag => exit_flag, &
                                  ROMS_noerror   => NoError
    use ocn_cplindices


    ! additional modules to interface with ROMS
    use roms_run_mod
    use ocn_io_tools
    use ocn_utils
    use ocn_timemgr_mod

    implicit none
!
!   !PUBLIC MEMBER FUNCTIONS:

    public :: ocn_init_mct
    public :: ocn_run_mct
    public :: ocn_final_mct

!   !PUBLIC DATA:
!
!
!EOP

!   !Save all variables in this scoping unit ????
    save

!   !Private by default
    private

!   !PRIVATE MODULE FUNCTIONS:

    ! --- Other parameters ---
    integer, parameter :: ocn_Success =  0, &   ! ocean success return code
                          ocn_Failure = -1     ! ocean failure return code

    character(len = *), parameter :: myModelName = 'roms'   !local model name

    ! --- Private variables ---

    integer :: ng       ! ROMS grid number
    integer :: nx_global, ny_global
!    integer :: inst_index               ! number of current instances (i.e. 1)
!    character(len = 16) :: inst_name    ! full name of current instance (i.e. "ocn_0001")
!    character(len = 16) :: inst_suffix  ! string associated with instance

    type(seq_infodata_type), pointer :: infodata


contains

!==============================================================================
!BOP
!   !ROUTINE: ocn_init_mct
!   !INTERFACE:

    subroutine ocn_init_mct(EClock, cdata, x2o, o2x, NLFilename)

!   !DESCRIPTION:
!       Initialize ROMS ocean model
!
!   !REVISION HISTORY:
!       Apr 23, 2012 - Raffaele Montuoro <rmontuoro@tamu.edu> - initial release
!
!   !USES:

        use mod_param,         only : Lm, Mm, Ngrids

        implicit none

!   !INPUT/OUTPUT PARAMETERS:

        type(ESMF_Clock)            , intent(in)    :: EClock
        type(seq_cdata)             , intent(inout) :: cdata
        type(mct_aVect)             , intent(inout) :: x2o, o2x
        character(len = *), optional, intent(in)    :: NLFilename

!EOP
!BOC
        !--- Local variables ---

        integer :: OCNID,      &
                   mpicom_o,   &
                   lsize,      &
                   nsend,      &
                   nrecv,      &
                   drv_cpl_dt,    &
                   ocn_cpl_dt,    &
                   ocn_start_tod, &
                   start_ymd,     &
                   start_year,    &
                   start_month,   &
                   start_day,     &
                   start_tod,     &
                   errorCode

        character (len = 32) :: starttype

        character (len = 8)  :: runtype

        type(mct_gsMap), pointer :: gsMap_o

        type(mct_gGrid), pointer :: dom_o

        character(len = *), parameter :: subname = 'ocn_init_mct'

        logical :: first = .true.

        !--- Begin

        ! --- Set cdata pointers ---

        call seq_cdata_setptrs(cdata,         ID=OCNID,  mpicom=mpicom_o, &
                               gsMap=gsMap_o, dom=dom_o, infodata=infodata)

        ! --- Initialize ROMS instance ---

        call roms_init(mpicom_o)

        ! --- Set shr logging to my log file ---

        call ocn_log_init(mpicom_o, OCNID)
        call ocn_set_logging(stdout)

        write(stdout,*) subname,' tcx0 ',ROMS_exit_flag

        ! --- Set field index values ---

        call ocn_cplindicesSet()

        ! --- Set start_type: initial, continue, branch ---
        ! *** will be extended in the future

        call seq_infodata_GetData(infodata, start_type=starttype)

        if (     trim(starttype) == trim(seq_infodata_start_type_start)) then
           runtype = 'initial'
        else if (trim(starttype) == trim(seq_infodata_start_type_cont) ) then
           runtype = 'continue'
           if (log_task) write(stdout,*) &
              'ocn_comp_mct WARNING: runtype ''continue'' not yet implemented'
        else if (trim(starttype) == trim(seq_infodata_start_type_brnch)) then
           runtype = 'branch'
           if (log_task) write(stdout,*) &
              'ocn_comp_mct WARNING: runtype ''branch'' not yet implemented'
        else
           write(stdout,*) 'ocn_comp_mct ERROR: unknown starttype'
           call ocn_abort(sigAbort, ' ocn_comp_mct ERROR: unknown starttype')
        end if

        ! --- Start timer ---

        call t_startf('roms_init')

        ! --- Start up ROMS ---

        write(stdout,*) subname,' tcx1 ',ROMS_exit_flag
        call roms_startup(nmlfile='ocn_in')
        if (ROMS_exit_flag /= ROMS_noerror) &
           call ocn_abort(sigAbort, 'ERROR in roms_init_state')
        write(stdout,*) subname,' tcx2 ',ROMS_exit_flag

        ! --- Read time parameters & initialize internal clock ---

        call ocn_timemgr_InitClock(mpicom_o, nmlfile = 'ocn_in')

        ! --- Reset number of ocean grids to 1 (to be extended) --

        Ngrids = 1      ! total grids
        ng     = 1      ! current grid #

        ! --- Set global grid size ---

        nx_global = Lm(ng)+2
        ny_global = Mm(ng)+2

        ! --- Stop timer ---

        call t_stopf('roms_init')

        ! Check for consistency of ROMS and sync clock initial time
        ! (perhaps not necessary: could set ROMS init time = cpl init time.)

!        if (runtype == 'initial') then
!           Check if driver start time == ROMS start time
!           *** Not sure if needed ***
!        end if

        ! --- Initialize MCT attribute vectors and indices ---

        call t_startf('roms_mct_init')

        write(stdout,*) 'calling ocn_setgsmap_mct',nx_global,ny_global
        call shr_sys_flush(stdout)

        write(stdout,*) subname,' tcx3 ',ROMS_exit_flag
        call ocn_SetGSMap_mct(mpicom_o, OCNID, GSMap_o)
        lsize = mct_gsMap_lsize(gsMap_o, mpicom_o)

        ! --- Initialize mct ocn domain ---

        write(stdout,*) subname,' tcx4 ',ROMS_exit_flag
        call ocn_domain_mct(lsize, gsMap_o, dom_o)

        ! --- Initialize mct attribute vectors ---

        call mct_aVect_init(x2o, rList=seq_flds_x2o_fields, lsize=lsize)
        call mct_aVect_zero(x2o)

        call mct_aVect_init(o2x, rList=seq_flds_o2x_fields, lsize=lsize)
        call mct_aVect_zero(o2x)

        nsend = mct_aVect_nRattr(o2x)
        nrecv = mct_aVect_nRattr(x2o)

        ! --- SBUFF is for accumulating exchanged fields to calculate averages ---
        ! --- NOT USED AT THE MOMENT ---
        !allocate (SBUFF_SUM(nx_block,ny_block,max_blocks_clinic,nsend)) !???

        ! --- Initialize coupling info ---

        ! --- Check for consistency of driver & ROMS coupling intervals ---

        write(stdout,*) subname,' tcx5 ',ROMS_exit_flag
        call seq_timemgr_EClockGetData(EClock, dtime=drv_cpl_dt)
        call ocn_timemgr_getClock( dtime=ocn_cpl_dt )
        if (mod(drv_cpl_dt, ocn_cpl_dt) /= 0) then
           write(stdout,*) ' drv_cpl_dt= ',drv_cpl_dt, &
                           ' ocn_cpl_dt= ',ocn_cpl_dt
           call ocn_abort(sigAbort,'ERROR roms_cpl_dt and ocn_cpl_dt not even multiples')
        end if

        ! --- Send initial state to driver ---
        ! --- accumulate data into buffer for averages (sum buffer) ---
        ! *** will be implemented later on

        write(stdout,*) subname,' tcx6 ',ROMS_exit_flag
        call ocn_export_mct(o2x, errorCode)
        if (errorCode /= ocn_Success) &
           call ocn_abort(sigAbort, 'ERROR in ocn_export_mct')

        call t_stopf('roms_mct_init')

        write(stdout,*) subname,' tcx7 ',ROMS_exit_flag
        call seq_infodata_PutData( infodata,           &
                                   ocn_nx = nx_global, &
                                   ocn_ny = ny_global )

        call seq_infodata_PutData( infodata,                 &
                                   ocn_prognostic   =.true., &
                                   ocnrof_prognostic=.true. )

        ! --- Reset shr logging to original units ---

        write(stdout,*) subname,' tcx9 ',ROMS_exit_flag
        call ocn_reset_logging

        ! --- Output delimiter to log file ---

        call ocn_log_delim (" End of initialization")

!EOC
    end subroutine ocn_init_mct

!==============================================================================
!BOP
!   !ROUTINE: ocn_run_mct
!
!   !DESCRIPTION:
!       Run ROMS for a coupling interval
!
!   !REVISION HISTORY:
!       Apr 23, 2012 - Raffaele Montuoro <rmontuoro@tamu.edu> - initial release
!       Apr 09, 2013 - Raffaele Montuoro <rmontuoro@tamu.edu> - rewrote set/reset logging
!
!   !INTERFACE:

    subroutine ocn_run_mct(EClock, cdata, x2o, o2x)

        implicit none

!   !INPUT/OUTPUT PARAMETERS:

        type(ESMF_Clock)            , intent(in)    :: EClock
        type(seq_cdata)             , intent(inout) :: cdata
        type(mct_aVect)             , intent(inout) :: x2o, o2x

!EOP
!BOC
        !--- Local variables ---

        integer :: drv_ymd, drv_tod, drv_year, drv_month, drv_day

        integer :: ymd, tod

        integer :: errorCode, rc

        real(r8) :: drv_curr_time,    &
                    ocn_run_interval, &
                    roms_StepTime

        character(len = 256 ) :: timestr

        type(ESMF_Time) :: drv_CurrTime, ocn_CurrTime

        character(len = *), parameter :: subname = 'ocn_run_mct'


        !--- Begin

        ! --- Reset shr logging to my log file ---

        call ocn_set_logging(stdout)
        write(stdout,*) subname,' tcx0 ',ROMS_exit_flag

        ! --- Get driver clock

        call ESMF_ClockGet(EClock,  CurrTime=drv_CurrTime, rc=rc)
        call ocn_timemgr_CheckRC(rc, clock_name='driver', callsub_name=subname)

        call ESMF_ClockGet(o_Clock, CurrTime=ocn_CurrTime, rc=rc)
        call ocn_timemgr_CheckRC(rc, clock_name='ocean ', callsub_name=subname)

        if (ocn_CurrTime > drv_CurrTime) then
           timestr = ''
           call ESMF_TimeGet( drv_CurrTime, timeString=timestr )
           call ocn_log (subname, 'driver current time', trim(timestr))
           timestr = ''
           call ESMF_TimeGet( ocn_CurrTime, timeString=timestr )
           call ocn_log (subname, 'ocean current time', trim(timestr))
           call ocn_abort(sigAbort, '('//trim(subname)//') ERROR: overshot coupling time')
        end if

        ! --- Advance the model in time over coupling interval

        write(stdout,*) subname,' tcx1 ',ROMS_exit_flag

        step_loop: do while (ocn_CurrTime < drv_CurrTime)

        write(stdout,*) subname,' tcx2 ',ROMS_exit_flag
            call roms_advance_step
            if (ROMS_exit_flag /= ROMS_noerror) &
               call ocn_abort(sigAbort, 'ERROR in roms_init_state')

        write(stdout,*) subname,' tcx3 ',ROMS_exit_flag
            call ocn_timemgr_advanceClock
            if (ROMS_exit_flag /= ROMS_noerror) &
               call ocn_abort(sigAbort, 'ERROR in roms_init_state')

        write(stdout,*) subname,' tcx4 ',ROMS_exit_flag
            call roms_set_state
        write(stdout,*) subname,' tcx5 ',ROMS_exit_flag
            if (ROMS_exit_flag /= ROMS_noerror) &
               call ocn_abort(sigAbort, 'ERROR in roms_init_state')

            call roms_radiation_stress
            if (ROMS_exit_flag /= ROMS_noerror) &
               call ocn_abort(sigAbort, 'ERROR in roms_radiation_stress')

            call ocn_import_mct(x2o, errorCode)
            if (errorCode /= ocn_Success) &
               call ocn_abort(sigAbort, 'ERROR in ocn_import_mct')

            call roms_step
            if (ROMS_exit_flag /= ROMS_noerror) &
               call ocn_abort(sigAbort, 'ERROR in roms_step')

            call ocn_export_mct(o2x, errorCode)
            if (errorCode /= ocn_Success) &
               call ocn_abort(sigAbort, 'ERROR in ocn_export_mct')

            call ESMF_ClockGet(o_Clock, CurrTime=ocn_CurrTime, rc=rc)
            call ocn_timemgr_CheckRC(rc, clock_name='ocean ', callsub_name=subname)

        end do step_loop

        ! --- Check that internal clock is in sync with master clock

        call ocn_timemgr_checkInSyncClock(EClock, name_SyncClock='driver')

        ! --- Reset shr logging to original units ---

        call ocn_reset_logging

!EOC
    end subroutine ocn_run_mct

!==============================================================================
!BOP
!   !ROUTINE: ocn_final_mct
!
!   !DESCRIPTION:
!       Properly finalize ROMS
!
!   !REVISION HISTORY:
!       Apr 23, 2012 - Raffaele Montuoro <rmontuoro@tamu.edu> - initial release
!       Apr 10, 2013 - Raffaele Montuoro <rmontuoro@tamu.edu> - added arguments
!
!   !INTERFACE:

    subroutine ocn_final_mct(EClock, cdata, x2o, o2x)

        implicit none

!   !INPUT/OUTPUT PARAMETERS:

        type(ESMF_Clock)            , intent(in)    :: EClock
        type(seq_cdata)             , intent(inout) :: cdata
        type(mct_aVect)             , intent(inout) :: x2o, o2x

!EOP
        ! --- Local variables ---

        character(len = *), parameter :: subname = 'ocn_final_mct'

!BOC
        call roms_finalize
!EOC
    end subroutine ocn_final_mct


!==============================================================================
!BOP
!   !ROUTINE: ocn_tile_bounds
!
!   !DESCRIPTION:
!       Get ROMS tile boundaries
!
!   !REVISION HISTORY:
!       Apr 23, 2012 - Raffaele Montuoro <rmontuoro@tamu.edu> - initial release
!
!   !INTERFACE:

    subroutine ocn_tile_bounds( ng, tile,           &
                                ibs, ibe, jbs, jbe, &
                                ids, ide, jds, jde, &
                                its, ite, jts, jte  )

        use mod_param, only : BOUNDS

        implicit none

!   !INPUT/OUTPUT PARAMETERS:
        integer, intent(in)  :: ng, tile
        integer, intent(out) :: ibs, ibe, jbs, jbe, &
                                ids, ide, jds, jde, &
                                its, ite, jts, jte

!EOP
        ! --- Local variables ---

        character(len = *), parameter :: subname = 'ocn_tile_bounds'

        !--- Begin

        ! --- Tile lower/upper bounds (include ghost points) ---

        ibs = BOUNDS(ng)%LBi(tile)
        ibe = BOUNDS(ng)%UBi(tile)
        jbs = BOUNDS(ng)%LBj(tile)
        jbe = BOUNDS(ng)%UBj(tile)

        ! --- Extended tile bounds ---
        ! --- Include boundary points if adjacent to physical boundary ---

        ids = BOUNDS(ng)%IstrR(tile)
        ide = BOUNDS(ng)%IendR(tile)
        jds = BOUNDS(ng)%JstrR(tile)
        jde = BOUNDS(ng)%JendR(tile)

        ! --- Interior tile bounds, used for computation ---

        its = BOUNDS(ng)%Istr(tile)
        ite = BOUNDS(ng)%Iend(tile)
        jts = BOUNDS(ng)%Jstr(tile)
        jte = BOUNDS(ng)%Jend(tile)


    end subroutine ocn_tile_bounds

!==============================================================================
!BOP
!   !ROUTINE: ocn_SetGSMap_mct
!
!   !DESCRIPTION:
!       Sets MCT global seg map for the domain decomposition
!
!   !REVISION HISTORY:
!       Apr 23, 2012 - Raffaele Montuoro <rmontuoro@tamu.edu> - initial release
!
!   !INTERFACE:

    subroutine ocn_SetGSMap_mct( mpicom_ocn, OCNID, gsMap_ocn )

        implicit none

        integer        , intent(in)    :: mpicom_ocn
        integer        , intent(in)    :: OCNID
        type(mct_gsMap), intent(inout) :: gsMap_ocn

        ! --- Local variables ---

        integer  :: i, j, lsize, n
        integer  :: ibs, ibe, jbs, jbe, &
                    ids, ide, its, ite, &
                    jds, jde, jts, jte

        integer, dimension(:), allocatable :: gindex

        character(len = *), parameter :: subname = 'ocn_SetGSMap_mct'

        ! --- Begin

        call ocn_tile_bounds( ng, ocn_tile, &
                              ibs, ibe, jbs, jbe, &
                              ids, ide, jds, jde, &
                              its, ite, jts, jte )

        lsize = (ide-ids+1) * (jde-jds+1)

        allocate(gindex(lsize))

        write(stdout,*) subname,'tcx1 ',ng,ocn_tile,ibs,ibe,jbs,jbe
        call shr_sys_flush(stdout)

        n=0
        do j=jds, jde
           do i=ids, ide
              n=n+1
              gindex(n) = i + 1 + j * nx_global
           end do
        end do

        call mct_gsMap_init( gsMap_ocn,  gindex, mpicom_ocn,  OCNID )

        deallocate(gindex)

    end subroutine ocn_SetGSMap_mct

!==============================================================================
!BOP
!   !ROUTINE: ocn_domain_mct
!   !INTERFACE:

    subroutine ocn_domain_mct( lsize, gsMap_ocn, dom_ocn )

!   !DESCRIPTION:
!       Sets MCT global seg map for the domain decomposition
!
!   !REVISION HISTORY:
!       Apr 23, 2012 - Raffaele Montuoro <rmontuoro@tamu.edu> - initial release
!
        use mod_grid,      only : GRID
        use shr_const_mod, only : SHR_CONST_PI, &
                                  SHR_CONST_REARTH

        implicit none

!   !INPUT PARAMETERS:

        integer        , intent(in)    :: lsize
        type(mct_gsMap), intent(in)    :: gsMap_ocn

!   !OUTPUT PARAMETERS:

        type(mct_gGrid), intent(inout) :: dom_ocn

!EOP
!BOC
        !--- Local variables ---
        integer :: i, ierr, imax, j, jmax, n
        integer :: ibs, ibe, jbs, jbe, &
                   ids, ide, its, ite, &
                   jds, jde, jts, jte

        integer , dimension(:), pointer :: idata
        real(r8), dimension(:), pointer :: data

        real(r8) :: dlon, latn, lats

        real(r8), parameter :: RAD_PER_DEG = SHR_CONST_PI/180.0_r8, &
                               REARTHINVSQ = 1.0_r8/(SHR_CONST_REARTH*SHR_CONST_REARTH)

        character (len = *), parameter :: subname = 'ocn_domain_mct'

        !--- Initialize mct domain type, lat/lon & mask

        call mct_gGrid_init( GGrid=dom_ocn,                         &
                             CoordChars=trim(seq_flds_dom_coord),   &
                             OtherChars=trim(seq_flds_dom_other),   &
                             lsize=lsize )

        call mct_aVect_zero( dom_ocn%data )
        allocate(data(lsize), stat=ierr)
        if (ierr > 0) call ocn_abort( sigAbort, &
           '('//trim(subname)//') ERROR: failed to allocate temporary workspace')

        !--- Determine global gridpoint number attribute, GlobGridNum

        call mct_gsMap_orderedPoints( gsMap_ocn, ocn_tile, idata )
        call mct_gGrid_importIAttr( dom_ocn, 'GlobGridNum', idata, lsize )

        !--- Initialize attribute vector with special value ---

        data(:) = -9999.0_r8
        call mct_gGrid_importRAttr( dom_ocn, "lat",   data, lsize )
        call mct_gGrid_importRAttr( dom_ocn, "lon",   data, lsize )
        call mct_gGrid_importRAttr( dom_ocn, "area",  data, lsize )
        call mct_gGrid_importRAttr( dom_ocn, "aream", data, lsize )

        data(:) = 0.0_r8
        call mct_gGrid_importRAttr( dom_ocn, "mask",  data, lsize )
        call mct_gGrid_importRAttr( dom_ocn, "frac",  data, lsize )

        !--- Fill in correct values ---

        call ocn_tile_bounds( ng, ocn_tile,       &
                              ibs, ibe, jbs, jbe, &
                              ids, ide, jds, jde, &
                              its, ite, jts, jte  )

        ! --- Latitude ---

        n=0
        do j=jds,jde
           do i=ids,ide
              n=n+1
              data(n) = GRID(ng)%latr(i,j)
           end do
        end do
        call mct_gGrid_importRAttr( dom_ocn, "lat",  data, lsize )

        ! --- Longitude ---

        n=0
        do j=jds,jde
           do i=ids,ide
              n=n+1
              data(n) = GRID(ng)%lonr(i,j)
           end do
        end do
        call mct_gGrid_importRAttr( dom_ocn, "lon",  data, lsize )

        ! --- Masking ---

        n=0
        do j=jds,jde
           do i=ids,ide
              n=n+1
              data(n) = GRID(ng)%rmask(i,j)
           end do
        end do
        call mct_gGrid_importRAttr( dom_ocn, "mask", data, lsize )

        ! --- Grid cells area ---

!       data(:) = 1._r8     ! temporary. Please check WRF areas and replace with code below

         n=0
         do j=jds,jde
            do i=ids,ide
               n=n+1
               data(n) = REARTHINVSQ / ( GRID(ng)%pm(i,j) * GRID(ng)%pn(i,j) )
            end do
         end do

        call mct_gGrid_importRAttr( dom_ocn, "area", data, lsize )

        ! --- Grid cell fraction (temporary) ---

        n=0
        do j=jds,jde
           do i=ids,ide
              n=n+1
              data(n) = GRID(ng)%rmask(i,j)
           end do
        end do

        call mct_gGrid_importRAttr( dom_ocn, "frac", data, lsize )

        deallocate(idata, data, stat=ierr )
        if (ierr > 0) call ocn_abort( sigAbort, &
           '('//trim(subname)//') ERROR: failed to deallocate temporary workspace')

!EOC

    end subroutine ocn_domain_mct

!==============================================================================
!BOP
!   !ROUTINE: ocn_import_mct
!
!   !INTERFACE:
    subroutine ocn_import_mct(x2o, errorCode)

!   !DESCRIPTION:
!       This routine sets ocean surface fluxes received from coupler for ROMS
!
!       Fluxes used:
!
!       o pslv  -- sea-level pressure                   (Pa)
!       o lwdn  -- longwave radiation (down)            (W/m2)
!       o lwup  -- longwave radiation (up)              (W/m2)
!       o swnet -- net shortwave heat flux              (W/m2)
!       o lat   -- latent heat flux                     (W/m2)
!       o sen   -- sensible heat flux                   (W/m2)
!       o melth -- heat flux from snow & ice melt       (W/m2)
!       o salt  -- salt flux                            (kg(salt)/m2/s)
!       o snow  -- water flux due to snow               (kg/m2/s)
!       o rain  -- water flux due to rain               (kg/m2/s)
!       o evap  -- evaporation flux                     (kg/m2/s)
!       o meltw -- snow melt flux                       (kg/m2/s)
!       o roff  -- river runoff flux                    (kg/m2/s)
!       o ioff  -- ice runoff flux                      (kg/m2/s)
!       o taux  -- zonal wind stress                    (Pa)
!       o tauy  -- meridional wind stress               (Pa)
!
!       All quantities are considered positive downward
!       e.g.: net heat flux = swnet + lwdn + lwup + sen + lat
!
!   !REVISION HISTORY:
!       Apr 23, 2012 - Raffaele Montuoro <rmontuoro@tamu.edu> - initial release
!
!   !USES:
        use mod_forces
        use mod_scalars,   only : isalt, itemp
        use shr_const_mod, only : SHR_CONST_CPSW,  &
                                  SHR_CONST_RHOFW, &
                                  SHR_CONST_RHOSW

        implicit none

!   !INPUT/OUTPUT PARAMETERS:

        type(mct_aVect), intent(inout) :: x2o

!   !OUTPUT PARAMETERS:

        integer        , intent(out)   :: errorCode       ! returned error code

!EOP
!BOC
        ! --- Local variables ---

        integer :: i, ierr, imax, j, jmax, n
        integer :: ibs, ibe, jbs, jbe, &
                   ids, ide, its, ite, &
                   jds, jde, jts, jte

        real(r8), dimension(:,:), allocatable :: taux, tauy

        real(r8), parameter :: conv_pair        = 0.01_r8,                                &
                               conv_heat_flux   = 1._r8/(SHR_CONST_RHOSW*SHR_CONST_CPSW), &
                               conv_water_flux  = -1._r8/SHR_CONST_RHOFW,                 &
                               conv_wind_stress = 0.5_r8/SHR_CONST_RHOSW

        character( len = * ), parameter :: subname = 'ocn_import_mct'

        ! --- Begin

        errorCode = ocn_Success        ! temporary

        ! --- Set surface fluxes for ROMS

        ! --- Get tile boundaries ---

        call ocn_tile_bounds( ng, ocn_tile,       &
                              ibs, ibe, jbs, jbe, &
                              ids, ide, jds, jde, &
                              its, ite, jts, jte )


        ! --- Initialize flux arrays ---

        ierr = 0
        allocate(taux(ibs:ibe,jbs:jbe), &
                 tauy(ibs:ibe,jbs:jbe), stat=ierr)
        if (ierr > 0) then
           errorCode = ocn_Failure
           call ocn_task_log( subname, 'ERROR: failed to allocate temporary workspace')
           return
        end if

        taux = 0.0_r8
        tauy = 0.0_r8

        FORCES(ng) % Pair (ibs:ibe,jbs:jbe)       = 0.0_r8
        FORCES(ng) % lrflx(ibs:ibe,jbs:jbe)       = 0.0_r8
        FORCES(ng) % srflx(ibs:ibe,jbs:jbe)       = 0.0_r8
        FORCES(ng) % lhflx(ibs:ibe,jbs:jbe)       = 0.0_r8
        FORCES(ng) % shflx(ibs:ibe,jbs:jbe)       = 0.0_r8
        FORCES(ng) % sustr(ibs:ibe,jbs:jbe)       = 0.0_r8
        FORCES(ng) % svstr(ibs:ibe,jbs:jbe)       = 0.0_r8
        FORCES(ng) % stflx(ibs:ibe,jbs:jbe,isalt) = 0.0_r8
        FORCES(ng) % stflx(ibs:ibe,jbs:jbe,itemp) = 0.0_r8


        ! --- Fields not yet used ---
        !
        !    x2o % rAttr(index_x2o_Foxx_melth, :)
        !    x2o % rAttr(index_x2o_Foxx_salt , :)
        !    x2o % rAttr(index_x2o_Foxx_snow , :)
        !    x2o % rAttr(index_x2o_Foxx_meltw, :)
        !    x2o % rAttr(index_x2o_Forr_roff , :)
        !    x2o % rAttr(index_x2o_Forr_ioff , :)


        n=0
        do j=jds,jde
           do i=ids,ide
              n=n+1

              ! --- surface air pressure (mb)
              FORCES(ng) % Pair (i,j) = conv_pair * x2o % rAttr(index_x2o_Sa_pslv, n)

              ! --- longwave net radiation flux (Celsius m/s)
              FORCES(ng) % lrflx(i,j) = conv_heat_flux * &
                                        (x2o % rAttr(index_x2o_Faxa_lwdn, n) + &
                                         x2o % rAttr(index_x2o_Foxx_lwup, n))

              ! --- shortwave net radiation flux (Celsius m/s)
              FORCES(ng) % srflx(i,j) = conv_heat_flux * x2o % rAttr(index_x2o_Foxx_swnet, n)

              ! --- latent net heat flux (Celsius m/s)
              FORCES(ng) % lhflx(i,j) = conv_heat_flux * x2o % rAttr(index_x2o_Foxx_lat, n)

              ! --- sensible net heat flux (Celsius m/s)
              FORCES(ng) % shflx(i,j) = conv_heat_flux * x2o % rAttr(index_x2o_Foxx_sen, n)

              ! --- surface salt flux (m/s), converted to psu m/s in set_vbc.F
              ! --- Note that fresh water flux is positive out of the ocean in ROMS
              ! --- Foxx_salt is the salt flux from melting ice (add later?)
              ! --- Foxx_prec is snow+rain (later?)
              ! --- Using evap-rain ---
              ! --- add prec(?)
              FORCES(ng) % rain (i,j) = x2o % rAttr(index_x2o_Faxa_rain, n)
              FORCES(ng) % stflx(i,j,isalt) = conv_water_flux * &
                                              (x2o % rAttr(index_x2o_Faxa_rain, n) + &
                                               x2o % rAttr(index_x2o_Foxx_evap, n))

              ! --- temperature kinematic surface flux (Celsius m/s)
              FORCES(ng) % stflx(i,j,itemp) = FORCES(ng) % srflx(i,j) + &
                                              FORCES(ng) % lrflx(i,j) + &
                                              FORCES(ng) % lhflx(i,j) + &
                                              FORCES(ng) % shflx(i,j)

              ! --- zonal & meridional wind stress (Pa)
              taux(i,j) = x2o % rAttr(index_x2o_Foxx_taux, n)
              tauy(i,j) = x2o % rAttr(index_x2o_Foxx_tauy, n)

           end do
        end do

        ! --- rotate wind stress from coupler to ROMS grid, if necessary ---
        call roms_rotate_vector(ng, ibs, ibe, jbs, jbe, taux, tauy, rot = -1.0_r8)

        ! --- performs halo update on rho grid points for wind stress ---
        call roms_halo_update_r2d(ng, ocn_tile,       &
                                  ibs, ibe, jbs, jbe, &
                                  taux, tauy)
        if (ROMS_exit_flag /= ROMS_noerror) then
           errorCode = ocn_Failure
           call ocn_task_log( subname, 'Error in wind stress halo update')
           return
        end if

        do j=jds,jde
           do i=its,ide
              FORCES(ng) % sustr(i,j) = conv_wind_stress * (taux(i-1,j)+taux(i,j))
           end do
        end do

        do j=jts,jde
           do i=ids,ide
              FORCES(ng) % svstr(i,j) = conv_wind_stress * (tauy(i,j-1)+tauy(i,j))
           end do
        end do

        deallocate(taux, tauy, stat=ierr)
        if (ierr > 0) then
           errorCode = ocn_Failure
           call ocn_task_log( subname, 'ERROR: failed to deallocate temporary workspace')
           return
        end if


        ! --- Update halo for all fluxes ---
        call roms_halo_update_fluxes(ng, ocn_tile,       &
                                     ibs, ibe, jbs, jbe, &
                                     FORCES(ng) % Pair , &
                                     FORCES(ng) % lrflx, &
                                     FORCES(ng) % srflx, &
                                     FORCES(ng) % lhflx, &
                                     FORCES(ng) % shflx, &
                                     FORCES(ng) % stflx, &
                                     FORCES(ng) % rain , &
                                     FORCES(ng) % sustr, &
                                     FORCES(ng) % svstr)

        if (ROMS_exit_flag /= ROMS_noerror) then
           call ocn_task_log( subname, 'Error in flux halo update')
           errorCode = ocn_Failure
           return
        end if

        ! --- Diagnostic
        if (diag_level > 0) call ocn_log(subname, ': received fluxes from coupler')
!EOC

    end subroutine ocn_import_mct


!==============================================================================
!BOP
!   !ROUTINE: ocn_export_mct
!
    subroutine ocn_export_mct(o2x, errorCode)

!   !DESCRIPTION:
!       This routine sends ROMS fields to the CPL7 coupler
!
!   !REVISION HISTORY:
!       Apr 23, 2012 - Raffaele Montuoro <rmontuoro@tamu.edu> - initial release
!
!   !USES:

        use mod_ocean,     only : OCEAN
        use mod_scalars,   only : itemp
        use mod_param,     only : N
        use mod_stepping,  only : nrhs
        use shr_const_mod, only : t0_kelvin => SHR_CONST_TKFRZ  ! T0 Kelvin

!
!   !INPUT PARAMETERS:

        type(mct_aVect), intent(inout) :: o2x

!   !OUTPUT PARAMETERS:

        integer        , intent(out)   :: errorCode       ! returned error code

!EOP
!BOC
        ! --- Local variables ---

        integer :: i, ierr, j, k, lu, lv
        integer :: ibs, ibe, jbs, jbe, &
                   ids, ide, jds, jde, &
                   its, ite, jts, jte, &
                   iue, iuw, jvn, jvs

        real(r8), dimension(:,:), allocatable :: u, v

        character( len = * ), parameter :: subname = 'ocn_export_mct'

        ! --- Begin

        errorCode = ocn_Success

        call ocn_tile_bounds( ng, ocn_tile,       &
                              ibs, ibe, jbs, jbe, &
                              ids, ide, jds, jde, &
                              its, ite, jts, jte )

        ! --- Surface temperature ---

        k=0
        do j=jds,jde
           do i=ids,ide
              k=k+1
              o2x % rAttr(index_o2x_So_t,k) = OCEAN(ng) % t(i,j,N(ng),nrhs(ng),itemp) &
                                            + t0_kelvin
           end do
        end do

        ! --- Surface zonal, meridional velocities ---

        ! --- allocate temporary workspace for u, v ---

        ierr = 0
        allocate(u(ids:ide,jds:jde), &
                 v(ids:ide,jds:jde), stat=ierr)
        if (ierr > 0) then
           errorCode = ocn_Failure
           call ocn_task_log( subname, 'ERROR: failed to allocate temporary workspace')
           return
        end if

        u = 0.0_r8
        v = 0.0_r8

        lu = nx_global-2
        lv = ny_global-2

        do j=jds,jde
           jvs = max(j,1)
           jvn = min(lv,j)+1
           do i=ids,ide
              iuw = max(i,1)
              iue = min(lu,i)+1
              u(i,j) = 0.5_r8 * (OCEAN(ng) % u(iuw,j,N(ng),2) + &
                                 OCEAN(ng) % u(iue,j,N(ng),2))
              v(i,j) = 0.5_r8 * (OCEAN(ng) % v(i,jvs,N(ng),2) + &
                                 OCEAN(ng) % v(i,jvn,N(ng),2))
           end do
        end do

        ! --- rotate ocean surface velocities to coupler grid, if necessary ---
        call roms_rotate_vector(ng, ids, ide, jds, jde, u, v, rot = +1.0_r8)

        ! --- exports ocean surface velocities ---

        k=0
        do j=jds,jde
           do i=ids,ide
              k=k+1
              o2x % rAttr(index_o2x_So_u,k) = u(i,j)
              o2x % rAttr(index_o2x_So_v,k) = v(i,j)
           end do
        end do

        ierr = 0
        deallocate(u, v, stat=ierr)
        if (ierr > 0) then
           errorCode = ocn_Failure
           call ocn_task_log( subname, 'ERROR: failed to deallocate temporary workspace')
           return
        end if

        ! --- Diagnostic ---
        if (diag_level > 0 ) call ocn_log(subname, ': sent fluxes to coupler')

!EOC

    end subroutine ocn_export_mct

!
end module ocn_comp_mct
